// Android Gradle Plugin out of the box only supports code coverage for instrumentation espresso) tests.
// This add support for unit tests as well.
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestDebugUnitTestCoverage(type: JacocoReport) {
    group = 'Reporting'
    description = "Generate Jacoco coverage reports for the Debug build. Only unit tests."

    reports {
        csv.enabled false
        xml.enabled = true
        html.enabled = true
    }

    // Make sure that tests from all modules are run before coverage report
    dependsOn ":app:testDebugUnitTest"
    dependsOn ":alibaba:testDebugUnitTest"
    dependsOn ":base:testDebugUnitTest"

    // what to exclude from coverage report, UI, "noise", generated classes, platform classes, etc.
    def excludes = [
            // Platform classes
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', 'androidx/**/*.*',
            // Ui, View, Adapter, Dialog, Application, HTTP
            '**/*Activity.*', '**/*Fragment.*', '**/*View.*', '**/*Adapter.*', '**/*Dialog.*', '**/ui/**', '**/activity/**', '**/fragment/**', '**/view/**', '**/adapter/**', '**/dialog/**', "**/ThisApplication.*", "**/network/**",
            // Anonymous inner class
            '**/*$*',
    ]

    // generated classes
    classDirectories.from = files([
            fileTree(
                    dir: "$project.rootDir/app/build/intermediates/javac/debug",
                    excludes: excludes),
            fileTree(
                    dir: "$project.rootDir/app/build/tmp/kotlin-classes/debug",
                    excludes: excludes),
            fileTree(
                    dir: "$project.rootDir/alibaba/build/intermediates/javac/debug",
                    excludes: excludes),
            fileTree(
                    dir: "$project.rootDir/alibaba/build/tmp/kotlin-classes/debug",
                    excludes: excludes),
            fileTree(
                    dir: "$project.rootDir/base/build/intermediates/javac/debug",
                    excludes: excludes),
            fileTree(
                    dir: "$project.rootDir/base/build/tmp/kotlin-classes/debug",
                    excludes: excludes)
    ])

    // sources
    sourceDirectories.from = fileTree(dir: project.rootDir, includes: [
            "app/src/main/java",
            "alibaba/src/main/java",
            "base/src/main/java"
    ])

    // execution
    executionData.from = fileTree(dir: project.rootDir, includes: [
            'app/build/jacoco/testDebugUnitTest.exec',
            'alibaba/build/jacoco/testDebugUnitTest.exec',
            'base/build/jacoco/testDebugUnitTest.exec'
    ])
}