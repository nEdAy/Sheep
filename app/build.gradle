apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'bugly.gradle'
apply from: 'mob.gradle'

Properties debugSigning = new Properties()
debugSigning.load(new FileInputStream(file(project.property("debug-signing.properties"))))

Properties releaseSigning = new Properties()
releaseSigning.load(new FileInputStream(file(project.property("release-signing.properties"))))

final VERSION_MAJOR = 3
final VERSION_MINOR = 0
final VERSION_PATCH = 0

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "cn.neday.sheep"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode Integer.parseInt(String.valueOf(VERSION_MAJOR) + String.valueOf(VERSION_MINOR).padLeft(2, "0") + String.valueOf(VERSION_PATCH).padLeft(3, "0"))
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
        resConfigs "zh"
    }
    signingConfigs {
        debug {
            storeFile file(debugSigning['storeFile'])
            storePassword debugSigning['storePassword']
            keyAlias debugSigning['keyAlias']
            keyPassword debugSigning['keyPassword']
        }
        release {
            storeFile file(releaseSigning['storeFile'])
            storePassword releaseSigning['storePassword']
            keyAlias releaseSigning['keyAlias']
            keyPassword releaseSigning['keyPassword']
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        // incremental true
        jumboMode true
        preDexLibraries true
        maxProcessCount 8
        // Instead of setting the heap size for the DEX process, increase Gradle's
        // heap size to enable dex-in-process. To learn more, read the next section.
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        // checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"

    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation 'com.google.android:flexbox:1.1.0'

    // ViewModel & LiveData & Lifecycle
    def lifecycle_version = "2.2.0-alpha02"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Bugly CrashReport & Upgrade
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.0'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'

    // Retrofit
    def retrofit_version = "2.6.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:4.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.0.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // Hawk secure key-value storage
    implementation "com.orhanobut:hawk:2.0.1"

    // BRVAH
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.47-androidx'

    // Glide
    def glide_version = "4.9.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    // kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Flyco UI
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'
    implementation 'com.flyco.banner:FlycoBanner_Lib:2.0.2@aar'
    implementation 'com.flyco.labelview:FlycoLabelView_Lib:1.0.2@aar'
    implementation 'com.flyco.dialog:FlycoDialog_Lib:1.3.2@aar'
    implementation 'com.flyco.roundview:FlycoRoundView_Lib:1.1.4@aar'

    // Particle Smasher
    implementation 'com.ifadai:particlesmasher:1.0.1'

    // Title UI
    implementation 'com.wuhenzhizao:titlebar:1.1.4'

    // Calligraphy
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'

    // Utils
    implementation 'com.blankj:utilcodex:1.24.7'

    // Analytics
    implementation 'com.umeng.umsdk:analytics:8.0.0'
    implementation 'com.umeng.umsdk:common:2.0.0'

    // 阿里百川
    // （必选）登陆授权 90kb + 11kb+ 62kb
    implementation 'com.ali.auth.sdk:alibabauth_core:1.6.1.4@jar'
    implementation 'com.ali.auth.sdk:alibabauth_ui:1.6.1.4@aar'
    implementation 'com.ali.auth.sdk:alibabauth_ext:1.6.1.4@jar'
    // （必选）基础安全组件，安全黑匣子（无线保镖），加密&解密，安全签名，风控相关内容；
    implementation 'com.taobao.android:securityguardaar3:5.4.94@aar'
    // （可选）安全高级，如使用了发放红包、优惠券高级功能，则必须引入
    // implementation 'com.taobao.android:securitybodyaar3:5.4.66@aar'
    // （必选）MTop，网关（稳定的高性能无线RPC网关，网关本身包含了一套安全体系，包括加密传输、防攻击、防刷、防篡改、过载保护等安全策略，
    implementation 'com.taobao.android:mtopsdk_allinone_open:1.3.1@jar'
    // （必选）UT，deviceID，主要用于计算设备的uttid，设备唯一标识 （67kb） // 和Umeng Sdk冲突
    implementation 'com.taobao.android:utdid4all:1.1.5.3_proguard@jar'
    // 联盟SDK
    implementation 'com.alimama.tunion.sdk:tuniontradesdk:0.3.2'
    // （必选）基础电商组件 包含打开detail,淘客分润,jsbridge注入等功能.(211kb)
    implementation 'com.alibaba.sdk.android:alibc_trade_common:3.1.1.221'
    implementation 'com.alibaba.sdk.android:alibc_trade_biz:3.1.1.221'
    implementation 'com.alibaba.sdk.android:alibc_trade_sdk:3.1.1.221'
    // （必选）AppLink，打通“手机淘宝”与三方app的桥梁，实现手机淘宝与三方app之间的往返跳转,包括登陆时跳手淘授权,用手淘打开商品页,店铺等(53kb)
    implementation 'com.alibaba.sdk.android:alibc_link_partner:1.1.0.6'
    // （必选）UT，用于组件、接口可用性统计（337kb）
    implementation 'com.alibaba.mtl:app-monitor-sdk:2.6.4.5_for_bc@jar'
    // （可选）支付，使用支付宝极简支付的isv需要该组件,否则会降级到h5支付,功能不会有影响,但是用户体验会降低(245kb)
    implementation 'com.alibaba.alipay:alipaySingle:20160825@jar'
    // Fastjson
    implementation 'com.alibaba:fastjson:1.2.57'

    // Android Debug Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // Chuck - http log/debug
    debugImplementation 'pl.droidsonroids:chuck:1.1.47'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'

    // Doraemonkit
    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:1.1.8'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:1.1.8'
}